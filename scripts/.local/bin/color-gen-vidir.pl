#!/usr/bin/env perl
# note: need an lf colors file to run this script
# an lf colors file can be generated by running
# color-gen-lf.pl
use strict;
use warnings;
use Getopt::Long;

my ($colors_file);
GetOptions(
    'colors|c=s' => \$colors_file,
) or die "Usage: $0 [--colors PATH]\n";

$colors_file ||= "$ENV{HOME}/.config/lf/colors";

die "Colors file not found: $colors_file\n"
    unless -f $colors_file;

my %COLORS = (
    '22;24;26'     => 'cd_bg',
    '30;33;36'     => 'cd_bg_alt',
    '60;64;72'     => 'cd_bg_highlight',
    '255;255;255' => 'cd_fg',
    '123;132;150' => 'cd_gray',
    '94;161;255'  => 'cd_blue',
    '94;255;108'  => 'cd_green',
    '94;241;255'  => 'cd_cyan',
    '255;110;94'  => 'cd_red',
    '241;255;94'  => 'cd_yellow',
    '255;94;241'  => 'cd_magenta',
    '255;94;160'  => 'cd_pink',
    '255;189;94'  => 'cd_orange',
    '189;94;255'  => 'cd_purple',
);

sub flag_to_gui {
    return (
        $_[0] == 1 ? 'bold' :
        $_[0] == 3 ? 'italic' :
        $_[0] == 4 ? 'underline' :
        $_[0] == 7 ? 'reverse' :
        $_[0] == 9 ? 'strikethrough' :
        'NONE'
    );
}

sub rgb_to_var {
    my ($r, $g, $b) = @_;
    return $COLORS{"$r;$g;$b"} // 'UNKNOWN';
}

open my $fh, '<', $colors_file or die "Cannot open $colors_file $!\n";

while (<$fh>) {
    chomp;
    next if /^\s*$/;

    my ($name, $codes) = split /\s+/, $_, 2;
    next unless defined $codes;

    my @codes = split /;/, $codes;

    my ($gui, $fg, $bg) = ('NONE', 'fg', 'NONE');

    for (my $i = 0; $i < @codes; $i++) {
        my $c = $codes[$i];

        if ($c =~ /^(0|1|3|4|7|9)$/) {
            $gui = flag_to_gui($c);
        }
        elsif ($c == 38 && $codes[$i+1] == 2) {
            $fg = rgb_to_var(@codes[$i+2 .. $i+4]);
            $i += 4;
        }
        elsif ($c == 48 && $codes[$i+1] == 2) {
            $bg = rgb_to_var(@codes[$i+2 .. $i+4]);
            $i += 4;
        }
    }

    printf "%s|%s|%s|%s\n", $name, $gui, $fg, $bg;
}

close $fh;
